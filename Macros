Dim accessToken

Sub CloneTemplate()
    accessToken = "5qt4vixlo6wpagao32yuerd6buwltj4shjcemyzztukhj2gsjwsq"
    
    Dim iteration As String
    iteration = InputBox("Enter Iteration Number (Example: 22.01)", "Clone Template", Default)
    
    If StrPtr(iteration) = 0 Or iteration = vbNullString Then
        Exit Sub
    Else
        ActiveSheet.Copy Before:=ActiveWorkbook.Sheets(1)
        ActiveSheet.Name = iteration
        
        Set_Buttons
        Update_PTSource
        Fetch_WorkItems
    End If
End Sub

Sub Set_Buttons()
    Dim shp As Shape
    For Each shp In ActiveSheet.Shapes
        If shp.TextFrame.Characters.text = "Clone" Then
            shp.Delete
        End If
    Next shp
End Sub

Sub Update_PTSource()
    Dim sheetName As String
    Dim tableName As String
    Dim pivotTableName As String
    Dim pivotSource As String
    
    sheetName = ActiveSheet.Name
    tableName = "Table" + sheetName
    pivotTableName = "Pivot" + sheetName
    pivotSource = tableName + "[[Points]:[Dev]]"
    
    With ActiveSheet
        .ListObjects(1).Name = tableName
        .PivotTables(1).Name = pivotTableName
        .PivotTables(1).ChangePivotCache _
            ActiveWorkbook.PivotCaches.Create( _
            SourceType:=xlDatabase, _
            SourceData:=pivotSource)
    End With
End Sub

Sub Fetch_WorkItems()
On Error GoTo Err
    Dim teamName As String
    Dim iteration As String
    Dim iterationPath As String
    
    'Set iteration info
    teamName = Split(ActiveWorkbook.Name, " ")(1)
    'iteration = CStr(Year(Now)) + " - " + Split(Worksheets(1).Name, ".")(1)
    iteration = "2023 - " + Split(Worksheets(1).Name, ".")(1)
    iterationPath = "Provider Services\\Iteration " + iteration + " " + teamName
    
    Dim organization As String
    Dim devopsBaseUrl As String
    Dim witUrl As String
    Dim witQuery As String
    Dim body As String
    
    'Form API request
    organization = "devops-iaa"
    devopsBaseUrl = "https://dev.azure.com/" + organization + "/_apis"
    witUrl = devopsBaseUrl + "/wit/wiql?api-version=6.0"
    witQuery = _
        "SELECT " _
        & "System.Id, System.Title, iaa.UserStory.BusinessPriority, Microsoft.VSTS.Scheduling.StoryPoints " _
        & "FROM workitems " _
        & "WHERE System.WorkItemType IN ('NonProd User Story', 'User Story', 'Bug') " _
        & "AND System.IterationPath UNDER '" & iterationPath & "' " _
        & "ORDER BY iaa.UserStory.BusinessPriority"
    body = "{ ""query"": """ & witQuery & """ }"
           
    'Send API request and parse info
    Dim objHTTP As Object
    Set objHTTP = CreateObject("MSXML2.ServerXMLHTTP.6.0")
    objHTTP.Open "POST", witUrl, False
    objHTTP.setRequestHeader "Content-Type", "application/json"
    objHTTP.setRequestHeader "Accept", "application/json"
    objHTTP.setRequestHeader "Authorization", "Basic " & EncodeBase64(accessToken + ":")
    objHTTP.send (body)
    
    If objHTTP.Status = 200 And Not objHTTP.responseText = vbNullString Then
        Dim response As String
        Dim iterationJson As Object
        Dim workItems As Object
        Dim idList As ArrayList
        Set idList = New ArrayList
        
        response = objHTTP.responseText
        Set iterationJson = JsonConverter.ParseJson(response)
        Set workItems = iterationJson("workItems")
                    
        For Each workItem In workItems
            idList.Add workItem("id")
        Next
        
        Call Fetch_WorkItemDetails(idList)
    End If
    
    Set objHTTP = Nothing
Err:
    'Error
    Set objHTTP = Nothing
    Exit Sub
End Sub

Sub Fetch_WorkItemDetails(idList As ArrayList)
On Error GoTo Err
    Dim teamName As String
    Dim iteration As String
    Dim iterationPath As String
    
    teamName = Split(ActiveWorkbook.Name, " ")(1)
    iteration = CStr(Year(Now)) + " - " + Split(Worksheets(1).Name, ".")(1)
    iterationPath = "Provider Services\\Iteration " + iteration + " " + teamName
    
    Dim organization As String
    Dim devopsBaseUrl As String
    Dim witUrl As String
    Dim witQuery As String
    Dim body As String
    
    organization = "devops-iaa"
    devopsBaseUrl = "https://dev.azure.com/" + organization + "/_apis"
    witUrl = devopsBaseUrl + "/wit/workitemsbatch?api-version=6.0&$expand=All"
    witQuery = _
        " ""System.Id"", ""System.Title"", ""iaa.UserStory.BusinessPriority"", ""Microsoft.VSTS.Scheduling.StoryPoints"" "
    body = "{ ""ids"": ["
    For Each ID In idList
        body = body + CStr(ID) + ","
    Next
    body = body + "], ""fields"": [ " & witQuery & " ] }"
    
    Dim objHTTP As Object
    Set objHTTP = CreateObject("MSXML2.ServerXMLHTTP.6.0")
    objHTTP.Open "POST", witUrl, False
    objHTTP.setRequestHeader "Content-Type", "application/json"
    objHTTP.setRequestHeader "Accept", "application/json"
    objHTTP.setRequestHeader "Authorization", "Basic " & EncodeBase64(accessToken + ":")
    objHTTP.send (body)

    If objHTTP.Status = 200 And Not objHTTP.responseText = vbNullString Then
        Dim response As String
        Dim iterationJson As Object
        Dim workItems As Object
        
        response = objHTTP.responseText
        Set workItemsJson = JsonConverter.ParseJson(response)
        Set workItems = workItemsJson("value")
        Call Set_UserStoryDetails(workItems)
    End If
Err:
    'Error
    Set objHTTP = Nothing
    Exit Sub
End Sub

Sub Set_UserStoryDetails(workItems As Object)
    Dim tableName As String
    Dim table As ListObject
    
    tableName = "Table" + ActiveSheet.Name
    Set table = ActiveSheet.ListObjects(tableName)

    'Empty existing rows
    If table.ListRows.Count > 0 Then
        table.DataBodyRange.Delete
    End If
    table.ListRows.Add
    Range("A2:H2").ClearFormats
    
    'Parse work item data and build out table rows & columns
    For Each workItem In workItems
        Dim fields As Object
        Dim lastRow As Integer
        
        Set fields = workItem("fields")
        lastRow = table.Range.Rows.Count - 1
        table.DataBodyRange.Cells(lastRow, 1).Value = fields("System.Id") 'US #
        table.DataBodyRange.Cells(lastRow, 2).Value = fields("System.Title") 'US title
        table.DataBodyRange.Cells(lastRow, 3).Value = fields("iaa.UserStory.BusinessPriority") 'US priority
        table.DataBodyRange.Cells(lastRow, 4).Value = fields("Microsoft.VSTS.Scheduling.StoryPoints") 'US points
        
        If Not table.ListRows.Count = workItems.Count Then
            table.ListRows.Add
        End If
    Next
End Sub

Function EncodeBase64(text As String) As String
    Dim arrData() As Byte
    arrData = StrConv(text, vbFromUnicode)
    
    Dim objXML As Object
    Dim objNode As Object

    Set objXML = CreateObject("MSXML2.DOMDocument")
    Set objNode = objXML.createElement("b64")
    
    objNode.DataType = "bin.base64"
    objNode.nodeTypedValue = arrData
    EncodeBase64 = Application.Clean(objNode.text)
    
    Set objNode = Nothing
    Set objXML = Nothing
End Function


-------------------------------------------------------------

referesh


Sub RefreshPivotTable()
    With ActiveSheet
        .PivotTables(1).RefreshTable
    End With
End Sub
